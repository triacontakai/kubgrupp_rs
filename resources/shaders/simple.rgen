#version 460

#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_ray_tracing : enable

#include "ray_common.glsl"
#include "raygen_common.glsl"

layout(location = 0) rayPayloadEXT RayPayload ray_info;

const float T_MIN = 0.0001;
const float T_MAX = 1000.0;

void main() {
    const vec2 pixel_center = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 in_uv = pixel_center / vec2(gl_LaunchSizeEXT.xy);

    vec2 d = in_uv * 2.0 - 1.0;

    vec4 origin = view_inverse * vec4(0, 0, 0, 1);
    vec4 target = proj_inverse * vec4(d.x, d.y, 1, 1);
    vec4 direction = view_inverse * vec4(normalize(target.xyz), 0);

    const uint ray_flags = gl_RayFlagsOpaqueEXT;

    traceRayEXT(
        tlas,
        ray_flags,
        0xFF,
        0,
        0,
        0,
        origin.xyz,
        T_MIN,
        direction.xyz,
        T_MAX,
        0
    );

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(ray_info.rad, 1.0));
}
